import os 


onsuccess:
    print("Workflow finished, no error")
    shell("send_message.py 'TreeMix simulation pipeline finished, no error'")

onerror:
    print("An error occurred, see the log")
    shell("send_message.py 'An error occured in TreeMix pipeline, see the log: {log}'")
    
onstart:
    print ("----------------------------------------------------------------------------")
    print ("Starting TreeMix on simulated data analysis pipeline")
    print ("----------------------------------------------------------------------------")
    shell('send_message.py "TreeMix pipeline started at $(date)"')

    
TreeMixBoot=range(1,101) # Number of bootstrap replicates (100)
TreeMixMigr=range(0,15) # Max number of Migration events to test
Chromosomes=map(lambda x: "chr"+str(x) , range(1,16+1)) # Chromosomes to use
#Models=["uniform", "uniform_different_groupsize",
#        "simple_bottleneck", "bottleneck_different_groupsize"]    

Models=["bottleneck_different_groupsize"]
Models=["uniform"]

rule all:
    input:
        "results/.setup_complete",
        #"simple_bottleneck_model.vcf",
         treeplots=expand("results/TreeMix/plots/Consensus_{model}_plots.pdf", model=Models),
         txt=expand("results/TreeMix/{model}-OptM.txt", model=Models)


rule setup:
    conda: "envs/vcftools.yaml"
    output: setup="results/.setup_complete"
           
    shell:
        """
        mkdir -p results/TreeMix/bootstrap logs/ misc/
        touch {output}        
        """         

         

rule build_model:
    output: "results/{model}-full.vcf.gz"
    conda: "envs/msprime.yaml"        
    params:
        isize=1e6,
        genyear=2900,
        rate=1.67e-10,
    script:
       "scripts/{wildcards.model}.py"


        


rule subsample_recode:
    conda: "envs/vcftools.yaml"
    input: vcf="results/{model}-full.vcf.gz",
    output: "results/{model}-filtered.vcf.gz"
    threads: 60
    params:
        nsnps=12000 #sample 12000 markers for speed
    message: "Generating random sample of SNPs and recoding for TreeMix"
    shell:            
        r"""
       
        #bcftools index -f  {input.vcf}
       
        set -x
        #set +o pipefail
        bcftools view --threads {threads} --with-header --types snps --max-alleles 2  {input.vcf} |\
        vcftools --gzvcf - --max-missing 1 --recode --stdout | bgzip -c > $$.tmp.vcf.gz
        bcftools view --header-only $$.tmp.vcf.gz > {output}.unsorted
        bcftools view --no-header $$.tmp.vcf.gz | awk '{{ printf("%f\t%s\n",rand(),$0);}}' | ( sort -t $'\t'  -T . -k1,1g || true) | head -n {params.nsnps} | cut -f 2- >> {output}.unsorted
        bcftools sort -o {output} {output}.unsorted 
       

        echo "---------------------------------------------------------"
        echo "Remaining markers after {rule}:"
        bcftools stats {output} | grep -e"^SN"
        rm -f  $$.tmp.vcf.gz  {output}.unsorted
        
        """

        
rule make_new_clusterfile:
    conda: "envs/vcftools.yaml"
    input:
        vcf="results/{model}-filtered.vcf.gz",
       

    output: clust="misc/{model}.clust",
            list="misc/{model}.list"
    message: "Generating new cluster file"
    priority: 0         
    shell:
        r"""
 
        bcftools query -l {input.vcf} | awk '{{ printf $1"\t"$1"\t"; gsub("_.*","",$1); printf $1"\n"}}' > {output.clust}
        bcftools query -l {input.vcf} | sort -u | awk '{{ gsub("_.*","",$1); printf $1"\n"}}' > {output.list}
        
        """


        
       


rule treemix_input:
    conda: "envs/vcftools.yaml"
    input: vcf="results/{model}-filtered.vcf.gz",       
   	   clust="misc/{model}.clust"
    output: "results/{model}-filtered.treemix.frq.gz"
    
    shell:
          """
          workflow/scripts/vcf2treemix.sh {input.vcf} {input.clust}
          """

### rule for generating a single bootstrap replicates
### bootstrap replicates will be run in parallel

rule:
    name: "TreeMix_bootstrap_{wildcards.model}_{wildcards.migrations}_{wildcards.replicate}" 
    conda: "envs/treemix.yaml"
    input: "results/{model}-filtered.treemix.frq.gz"
    output:
        treeout="results/TreeMix/bootstrap/{model}/bootrep_m{migrations}_b{replicate}.treeout.gz",
            cov="results/TreeMix/bootstrap/{model}/bootrep_m{migrations}_b{replicate}.cov.gz",
            modelcov="results/TreeMix/bootstrap/{model}/bootrep_m{migrations}_b{replicate}.modelcov.gz",
            llik="results/TreeMix/bootstrap/{model}/bootrep_m{migrations}_b{replicate}.llik"
            #dir=directory("TreeMix/bootstrap/{myfilter}")
    threads: 1
    retries: 5
    resources:
        nr = lambda wildcards, attempt: attempt,
        runtime = "24h"
        
    log: "logs/bootrep_{model}_m{migrations}_b{replicate}.log"
    params:
        prefix="results/TreeMix/bootstrap/{model}/bootrep_m{migrations}_b{replicate}"
        
    message:
        "TreeMix boostrap replicate no. {wildcards.replicate} - Model: {wildcards.model} migrations: {wildcards.migrations} max runtime per job: {resources.runtime} min, try {resources.nr} of 5"    
    shell:
        r"""
        mkdir -p results/TreeMix/bootstrap/{wildcards.model} 
        SEED=$RANDOM
        echo "SEED=$SEED" > {log}
        #echo calculating bootstrap replicate  {wildcards.replicate} for {wildcards.model} with {wildcards.migrations} migrations, seed: $SEED
        echo 
        timeout --kill-after=5m {resources.runtime}m treemix -i {input} -bootstrap -se -k 500 -m {wildcards.migrations} -root Wild -seed $SEED -o {params.prefix} >> {log} 2>&1
        echo finished bootstrap replicate  {wildcards.replicate}, {wildcards.model}, {wildcards.migrations} migrations, attempts: {resources.nr}
        
        """

        # put all trees into a single file

rule:
    name: "merge_replicates_{wildcards.model}_{wildcards.migrations}"
    input:
        expand("results/TreeMix/bootstrap/{{model}}/bootrep_m{{migrations}}_b{replicate}.treeout.gz",
               replicate=TreeMixBoot)
    output:
        "results/TreeMix/alltrees_{model}_m{migrations}.trees"
    message: "Merging replicates for {wildcards.model}, migrations {wildcards.migrations}"
    shell:
         """
         for f in {input}
         do
           zcat $f | head -1 >> {output}
         done
         """

rule OptM:
    conda: "envs/r-optm.yaml"
    input: expand("results/TreeMix/bootstrap/{{model}}/bootrep_m{migrations}_b{replicate}.llik", migrations=TreeMixMigr, replicate=TreeMixBoot)
    output: plot="results/TreeMix/plots/{model}-OptM-plot.pdf",
            txt="results/TreeMix/{model}-OptM.txt"
    params: folder="results/TreeMix/bootstrap/{model}"        
    shell:
        r"""
        mkdir -p results/TreeMix/plots
cat <<'EOF' > {rule}.$$.tmp.R

library(OptM)
test.optM <- optM('{params.folder}')
write.table(test.optM, file='{output.txt}', sep="\t", quote=F)
### plot needs to be set to FALSE if not in interactive mode        
plot_optM(test.optM, method = "Evanno", plot=FALSE, pdf='{output.plot}')

EOF
        
        Rscript {rule}.$$.tmp.R
        rm {rule}.$$.tmp.R
        
        """

rule plot_trees:
    conda: "envs/treemix.yaml"
    input:
        cons=expand("results/TreeMix/Consensus_{{model}}_m{migrations}.treeout.gz", migrations=TreeMixMigr),
        list="misc/{model}.list"
    output:
        pdf="results/TreeMix/plots/Consensus_{model}_plots.pdf"
    params:
        ms=TreeMixMigr,
        prefix="results/TreeMix/Consensus_{model}_m"

    shell:
        r"""
mkdir -p TreeMix/plots
cat <<'EOF' > {rule}.$$.tmp.R

library(RColorBrewer)
source("workflow/scripts/plotting_funcs.R")        
mrange <- seq({params.ms}[1], {params.ms}[2]-1)         
prefix <- '{params.prefix}'

pdf(file='{output.pdf}')
        
for (m in mrange) {{
 print(paste("plotting M=",m))       
 x0=plot_tree(cex=0.9,  paste0(prefix,m))
 title(paste('{wildcards.model}', m,"edges"))
 #x1=plot_resid(stem=paste0(prefix,m), pop_order="{input.list}")
}}      
dev.off()
        
        
EOF

  Rscript {rule}.$$.tmp.R
  rm {rule}.$$.tmp.R
        
        
        """
 
rule consensus:
    conda: "envs/phylip.yaml"
    input: "results/TreeMix/alltrees_{model}_m{migrations}.trees"
    output: "results/TreeMix/{model}_m{migrations}.contree"
    params:
        wdir="results/TreeMix/{model}_m{migrations}_wdir"
    shell:
        """
        set -x
        mkdir -p {params.wdir}
        IN=$(realpath {input})
        OUT=$(realpath {output})
        posOutgroup=`head -1 {input} | tr "," "\n" | grep "Wild" -n | cut -d":" -f1`
        cd {params.wdir}

	# echo $posOutgroup
	echo $IN > pconf
	echo "O" >> pconf
	echo $posOutgroup >> pconf
	echo "Y" >> pconf
        consense < pconf > consense.out
        cat outtree | tr -d "\n" > $OUT
        echo >> $OUT
        cd ../..
        rm -rf {params.wdir}/
        """
  
rule final_tree_from_consensus:
    conda: "envs/treemix.yaml"
    input:
        contree="results/TreeMix/{model}_m{migrations}.contree",
        frq="results/{model}-filtered.treemix.frq.gz"
    output: treeout="results/TreeMix/Consensus_{model}_m{migrations}.treeout.gz",
            cov="results/TreeMix/Consensus_{model}_m{migrations}.cov.gz",
            modelcov="results/TreeMix/Consensus_{model}_m{migrations}.modelcov.gz"
    params:
        prefix="results/TreeMix/Consensus_{model}_m{migrations}"
    log: "logs/Consensus_{model}_m{migrations}.log"
    message: "Computing final consensus tree for {wildcards.model}, migrations: {wildcards.migrations}"     
    shell:
        """
        treemix -i {input.frq} -m {wildcards.migrations} -k 500 -root Wild \
        -se -tf {input.contree} -o {params.prefix} > {log}

        
        """
        
